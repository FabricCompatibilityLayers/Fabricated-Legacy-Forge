import com.diffplug.gradle.spotless.JavaExtension

plugins {
	id 'com.diffplug.spotless' version "6.12.0"
	id 'fabric-loom' version "1.1-SNAPSHOT" // To use chocoloom, change the version to a commit hash
	id 'legacy-looming' version "1.1-SNAPSHOT" // Version must be the same as fabric-loom's
	id 'maven-publish'
	id "com.modrinth.minotaur" version "2.+"
}

sourceCompatibility = JavaVersion.VERSION_1_8
targetCompatibility = JavaVersion.VERSION_1_8

archivesBaseName = project.archives_base_name + "-" + project.minecraft_version
version = project.mod_version
def displayVersion = version + "+" + project.minecraft_version
group = project.maven_group

def debug = true
def branch = System.getenv("GITHUB_REF_NAME") == null ? "-forge" : System.getenv("GITHUB_REF_NAME")
if (!branch.endsWith("-forge")) {
	debug = false
}

repositories {
	maven {
		name = "Modrinth"
		url = "https://api.modrinth.com/maven"
		content {
			includeGroup "maven.modrinth"
		}
	}
	flatDir {
		dirs "./lib"
	}

	maven {
		name = "Jitpack"
		url 'https://jitpack.io/'
	}
}

loom {
	setIntermediaryUrl('https://repo.legacyfabric.net/repository/legacyfabric/net/legacyfabric/intermediary/%1$s/intermediary-%1$s-v2.jar');

	accessWidenerPath = file("src/main/resources/fabricated-forge.accesswidener")
}

dependencies {
	minecraft "com.mojang:minecraft:${project.minecraft_version}"
	mappings "net.legacyfabric:yarn:${project.minecraft_version}+build.${project.yarn_mappings}:v2"
	modImplementation "net.fabricmc:fabric-loader:${project.loader_version}"

	modApi("maven.modrinth:mod-remapping-api:${project.mod_remapping_api}")

	modImplementation(legacyFabricApi.module("legacy-fabric-logger-api-v1", "1.9.0+1.8.9"))

	implementation(include(group: "local", name: "argo", version: "2.25"))
	implementation(include(group: "local", name: "bcprov-jdk15on", version: "147"))
	implementation(include(group: "local", name: "guava", version: "12.0.1"))

	//modRuntimeOnly(files("./forge-${project.minecraft_version}-${project.forge_version}-universal-intermediary.jar"))
}

processResources {
	inputs.property "version", project.version

	filesMatching("fabric.mod.json") {
		expand "version": project.version
	}
}

// ensure that the encoding is set to UTF-8, no matter what the system default is
// this fixes some edge cases with special characters not displaying correctly
// see http://yodaconditions.net/blog/fix-for-java-file-encoding-problems-with-gradle.html
tasks.withType(JavaCompile).configureEach {
	it.options.encoding = "UTF-8"
	if (JavaVersion.current().isJava9Compatible()) it.options.release = 8
}

java {
	// Loom will automatically attach sourcesJar to a RemapSourcesJar task and to the "build" task
	// if it is present.
	// If you remove this line, sources will not be generated.
	withSourcesJar()
}

jar {
	from("licensing.md")
	from("LICENSE") {
		rename { "${it}_${project.archivesBaseName}" }
	}
	from("LICENSE-fml.txt")
	from("CREDITS-fml.txt")
	from("MinecraftForge-Credits.txt")
	from("MinecraftForge-License.txt")
}

// configure the maven publication
publishing {
	publications {
		mavenJava(MavenPublication) {
			from components.java
		}
	}

	// select the repositories you want to publish to
	repositories {
		// uncomment to publish to the local maven
		// mavenLocal()
	}
}

spotless {
	format('fml', JavaExtension) {
		licenseHeaderFile(rootProject.file("HEADER_FML"))
		target 'src/*/java/cpw/mods/**/*.java', 'src/*/java/net/minecraft/*.java'
	}

	format('forge', JavaExtension) {
		licenseHeaderFile(rootProject.file("HEADER_FORGE"))
		target 'src/*/java/net/minecraftforge/**/*.java'
	}
}

modrinth {
	token = System.getenv("MODRINTH_TOKEN") == null ? "dev" : System.getenv("MODRINTH_TOKEN") // This is the default. Remember to have the MODRINTH_TOKEN environment variable set or else this will fail, or set it to whatever you want - just make sure it stays private!
	projectId = "JpHZ1Cyv" // This can be the project ID or the slug. Either will work!
	versionName = displayVersion
	changelog = "Port to Minecraft 1.4.7.\nWith Forge 6.6.2.534 and FML 4.7.35.556."
	uploadFile = remapJar
	gameVersions = [project.minecraft_version] // Must be an array, even with only one version
	debugMode = debug
	dependencies { // A special DSL for creating dependencies
		// scope.type
		// The scope can be `required`, `optional`, `incompatible`, or `embedded`
		// The type can either be `project` or `version`
		required.version "eRGeQXjD", project.mod_remapping_api
	}
}
